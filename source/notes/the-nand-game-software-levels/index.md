---
title: The Nand Game - Software Levels
notebook: notes
tags:
- game/puzzle
- it/hardware
mathjax: true
date: 2025-07-15 23:38:35
updated: 2025-07-15 23:38:35
---
## All Levels

- [The Nand Game](../the-nand-game/index.md) - Hardware Levels
- [The Nand Game - Software Levels](../the-nand-game-software-levels/index.md)
- [The Nand Game - Optional Levels](../the-nand-game-optional-levels/index.md)

## Assembler Quick Reference

Syntax: `destination = calculation ; jump-condition`. Destination and jump-condition are optional.

**D** and **A** are the two registers. **\*A** indicate the value in RAM at the address contained in **A**.

Examples:

- `D = D + 1`: Calculate D + 1 and store the result in D.
- `D - 1 ; JGE`: Calculate D - 1. Jump if the result is greater than 0. (Result is not stored.)
- `A = 1234`: Store the number 1234 in the A register.
- `# Hello`: Comment.

### Calculations

- `D + A`
- `D - A` or `A - D`
- `D & A` (Bitwise and)
- `D | A` (Bitwise or)
- `A + 1` or `D + 1`
- `A - 1` or `D - 1`
- `~A` or `~D` (Bitwise inversion)
- `1` or `0` or `-1`

In all cases can **\*A** be used instead of **A**.

### Destinations

Any combination of **D**, **A** and **\*A** can be used as destination. Multiple destinations are comma separated, e.g. `D, A = 1`.

### Jump conditions

Jump when result of calculations is:

- `JQE` equal to 0
- `JNE` not equal to 0
- `JGT` greater than 0
- `JGE` greater than or equal to 0
- `JLT` less than 0
- `JLE` less than or equal to 0
- `JMP` unconditional jump (regardless of the calculation result)

### Number instructions

**A** can be directly assigned a number. Example: `A = 47`.

Numbers in hexadecimal is prefixed with `0x`, e.g.: `A = 0x7FFF`.

Numbers in binary is prefixed with `0b`, e.g.: `A = 0b010111`. Underscores can be used to separate digit group, e.g. `A = 0b_0101_1100`.

### Comments

Lines starting with `#` are ignored by the assembler. They can be used for comments and documentation.

### Labels

The keyword **LABEL** followed by a name makes the name represent the address of the following instruction. This address can be assigned to **A** before a jump, e.g. `A = LOOP`.

### Defines

The keyword **DEFINE** followed by a name and a number will cause the name to be replaced with the number when it occurs in other instructions. For example:

``` nasm
DEFINE foo 0x7FFF
A = foo
```

Is equivalent to: `A = 0x7FFF`.

### Multiple targes

It is possible to assign a result to multiple register.

E.g. `D, A = D - *A`. The result of calculation will be written to both A and D.

Any combination of A, D, and \*A can be specified as destination.

## Software Level: Low level

### Machine code

Write a program with four instructions:

0) Set the D register to 0
1) Set the A register to 2
2) Add 1 to the D register
3) Jump unconditionally

å°±æ˜¯åœ¨ Hardware æœ€å [Computer](../the-nand-game/index.md#Computer) é‚£å…³çš„ ROM é‡Œå¡«å…¥æŒ‡ä»¤ã€‚

> The word at the PC address in the program memory is theÂ **I**Â input to the control unit.

éœ€è¦ç»“åˆ [Control Unit](../the-nand-game/index.md#Control%20Unit)ã€ [ALU Instruction](../the-nand-game/index.md#ALU%20Instruction)ã€[ALU](../the-nand-game/index.md#ALU) å’Œ [Condition](../the-nand-game/index.md#Condition) çœ‹æŒ‡ä»¤æ¯ä¸€ä½çš„å–å€¼ã€‚

> Note: Inside ALU instruction: `alu(X=D, Y=A/*A)`.

| I: Bit | I: Hex | Group     | Flag | Note                    |
| ------ | ------ | --------- | ---- | ----------------------- |
| 15     | 0x8000 | -         | ci   | 0: data (to A); 1: ALU  |
| 12     | 0x1000 | -         | \*   | 1: use \*A instead of A |
| 10     | 0x400  | operation | u    |                         |
| 9      | 0x200  | operation | op1  |                         |
| 8      | 0x100  | operation | op0  |                         |
| 7      | 0x80   | operation | zx   | 1: D = 0                |
| 6      | 0x40   | operation | sw   | 1: swap D and A         |
| 5      | 0x20   | target    | a    | 1: a                    |
| 4      | 0x10   | target    | d    | 1: d                    |
| 3      | 0x8    | target    | \*a  | 1: \*a                  |
| 2      | 0x4    | jump      | lt   |                         |
| 1      | 0x2    | jump      | eq   |                         |
| 0      | 0x1    | jump      | gt   |                         |

| u   | op1 | op0 | Output   |
| --- | --- | --- | -------- |
| 0   | 0   | 0   | D and A  |
| 0   | 0   | 1   | D or A   |
| 0   | 1   | 0   | D xor A  |
| 0   | 1   | 1   | invert D |
| 1   | 0   | 0   | D + A    |
| 1   | 1   | 0   | D - A    |
| 1   | 0   | 1   | D + 1    |
| 1   | 1   | 1   | D - 1    |

|lt|eq|gt| output 1 when |
| --- | --- | --- | ------------- |
|0|0|0| Never         |
|0|0|1| X > 0         |
|0|1|0| X = 0         |
|0|1|1| X â‰¥ 0         |
|1|0|0| X < 0         |
|1|0|1| X â‰  0         |
|1|1|0| X â‰¤ 0         |
|1|1|1| Always        |

#### Set the D register to 0

- å¾€å¯„å­˜å™¨ D å†™æ•°æ®ï¼Œå¾—ç”¨ alu instructionï¼Œ`ci = 1`ï¼Œ`target.d = 1`ã€‚
- è¦å†™å…¥ 0ï¼Œé‚£ä¹ˆ alu æ¨¡å—åº”è¯¥å– `u = 0, op1 = 0, op0 = 0` å³ `D and A` ï¼ŒåŒæ—¶ `zx = 1` å³ `D = 0`ï¼ˆåˆèµ·æ¥å°±æ˜¯ `0 and A = 0`ã€‚
- condition æ¨¡å—å–å…¨é›¶å³è¾“å‡ºæ’ 0ã€‚

So `I = ci | d | zx = 0x8090` (i.e. `D = 0`).

#### Set the A register to 2

- å¾€å¯„å­˜å™¨ A å†™æ•°æ®ï¼Œè®¾ç½® `ci = 0`ï¼Œå…¶ä½™ä½æ„æˆè¦å†™å…¥çš„æ•°å­— 2ã€‚

So `I = 0x0002` (i.e. `A = 2`).

#### Add 1 to the D register

- éœ€è¦ä½¿ç”¨ alu instructionï¼Œè®¾ç½® `ci = 1`ï¼Œ`target.d = 1`ã€‚
- alu æ¨¡å—å– `u = 1, op1 = 0, op0 = 1` å³ `D + 1`ã€‚
- condition æ¨¡å—å…¨é›¶ã€‚

So `I = ci | d | u | op0 = 0x8510` (i.e. `D = D + 1`).

#### Jump unconditionally

- éœ€è¦ä½¿ç”¨ alu instructionï¼Œè®¾ç½® `ci = 1`ã€‚
- condition æ¨¡å—å–å…¨ä¸€å³è¾“å‡ºæ’ 1ã€‚
  - æ³¨æ„å½“ `j`ï¼ˆå³ condition çš„è¾“å‡ºï¼‰ä¸º 1 æ—¶ï¼ŒPC ä¼šè¢«è®¾ç½®ä¸ºå¯„å­˜å™¨ A çš„å€¼ã€‚æœ¬é¢˜ä¸­ä¼šè·³åˆ° `ROM[2]`ã€‚

So `I = ci | lt | eq | gt = 0x8007` (i.e. `JMP`).

#### All together

{% invert %}

![|800](20250617-005346.png)

{% endinvert %}

æŒç»­è¿è¡Œçš„æ•ˆæœç›¸å½“äºå¯¹å¯„å­˜å™¨ D ä» 0 å¼€å§‹é€æ­¥é€’å¢ã€‚

### Assembly Language

Programming a computer by directly setting instruction bits is quite time-consuming and error-prone.

Therefore we create a so-calledÂ **assembler**Â language, which is a text-based format using letters and symbols instead of bits to represent machine-code instructions.

AnÂ **assembler**Â translates the symbolic instructions into binary machine code.

An assembler instruction has three parts: TheÂ _destination_, theÂ _calculation_Â and an (optional)Â _jump-condition_.

- The destination is the register(s) which the output of the operation is written to.
- The calculation is the ALU operation.
- The jump condition is the condition which will cause a jump.

Example instruction:

$$
\underbrace{D=}_{\text{ Destination }} \underbrace{D+1}_{\text{ Calculation }} \underbrace{\text{; JNE}}_{\text{ Jump-condition }}
$$

#### Destination

> ç”¨ç©ºç™½ä»£è¡¨ 0ï¼Œè®© 1 æ›´çªå‡ºã€‚

| Opcode      | a   | d   | \*a |
| ----------- | --- | --- | --- |
| (blank)     |     |     |     |
| A =         | 1   |     |     |
| D =         |     | 1   |     |
| \*A =       |     |     | 1   |
| A, D =      | 1   | 1   |     |
| D, \*A =    |     | 1   | 1   |
| A, D, \*A = | 1   | 1   | 1   |
| A, \*A =    | 1   |     | 1   |

#### Calculation

æœ‰äº› operation å¯ä»¥å¯¹åº”å¤šç§ä¸åŒçš„ flags ç»„åˆã€‚

| Opcode | u   | op1 | op0 | zx  | sw  | Note     |
| ------ | --- | --- | --- | --- | --- | -------- |
| D+A    | 1   |     |     |     |     |          |
| D-A    | 1   | 1   |     |     |     |          |
| A-D    | 1   | 1   |     |     | 1   |          |
| D+1    | 1   |     | 1   |     |     |          |
| A+1    | 1   |     | 1   |     | 1   |          |
| D-1    | 1   | 1   | 1   |     |     |          |
| A-1    | 1   | 1   | 1   |     | 1   |          |
| -D     | 1   | 1   |     | 1   | 1   | i.e. 0-D |
| -A     | 1   | 1   |     | 1   |     | i.e. 0-A |
| -1     | 1   | 1   | 1   | 1   |     | i.e. 0-1 |
| 1      | 1   |     | 1   | 1   |     | i.e. 0+1 |
| D      | 1   |     |     | 1   | 1   | i.e. 0+D |
| A      | 1   |     |     | 1   |     | i.e. 0+A |
| D&A    |     |     |     |     |     |          |
| D\|A   |     |     | 1   |     |     |          |
| ~D     |     | 1   | 1   |     |     |          |
| ~A     |     | 1   | 1   |     | 1   |          |
| 0      |     |     |     | 1   |     | i.e. 0&A |

#### Jump-condition

| Opcode  | lt  | eq  | gt  |
| ------- | --- | --- | --- |
| (blank) |     |     |     |
| ; JLT   | 1   |     |     |
| ; JEQ   |     | 1   |     |
| ; JGT   |     |     | 1   |
| ; JLE   | 1   | 1   |     |
| ; JGE   |     | 1   | 1   |
| ; JMP   | 1   | 1   | 1   |
| ; JNE   | 1   |     | 1   |

### Assembler program

Write a program in assembler which causes the lamp to blink at least three times.

The lamp is memory-mapped to the address 7FFF, bits 1 and 0.

| Bit | Set to 1 to:  |
| --- | ------------- |
| 0   | Turn lamp on  |
| 1   | Turn lamp off |

The external device is only affected when a bit is changing from 0 to 1.

ç»™å†…å­˜åœ°å€ 0x7FFF å†™å…¥ 0x1ï¼Œåˆ™ç¯äº®ï¼›ç›´æ¥å¯¹æ­¤æ•°å€¼å–åï¼Œåˆ™ç¯ç­ã€‚

``` nasm
# 0x7FFF
A = 0x7FFF
# 0xe588 = ci3 | u | op0 | zx | *a
*A = 1
# 0xf348 = ci3 | * | op1 | op0 | sw | *a
*A = ~*A
*A = ~*A
*A = ~*A
*A = ~*A
*A = ~*A
```

ğŸ‘† å…¶ä¸­ `ci3` è¡¨ç¤ºæœ€é«˜çš„ 3 ä½ï¼ˆ`ci` åŠå…¶å³è¾¹çš„ä¸¤ä½ï¼‰ã€‚

### Keyboard input

Write a program in assembler which write keyboard input into memory.

The keyboard input is memory-mapped to addressÂ `6000`.

Write the first character typed at memory addressÂ `1000`(hex), the second typed atÂ `1001`(hex) and so on.

Note: A key will usually be held down for much longer than a clock cycle, but should only be registered as a single input until the key is released.

ä¸»è¦çš„éº»çƒ¦æ˜¯å¯„å­˜å™¨å¤ªå°‘â€¦â€¦å¾—ç”¨å†…å­˜æ¥ä¿å­˜å˜é‡ã€‚

éœ€è¦è®°å½•ä¸‹ä¸€æ¬¡è¦å†™å…¥çš„å†…å­˜åœ°å€ï¼ˆèµ·å§‹å€¼ä¸º 0x1000ï¼‰ï¼Œç”¨å†…å­˜åœ°å€ 0xFFF æ¥è®°å½•æ­¤å€¼ã€‚

``` nasm
DEFINE KBD 0x6000
DEFINE W_ADDR 0xFFF

# Variable: the writing addr
A = 0x1000
D = A
A = W_ADDR
*A = D

# Waiting for key press
LABEL WAIT_KEY_PRESS
A = KBD
D = *A
A = WAIT_KEY_PRESS
D ; JEQ

# Some key is pressing
# Write it to next free memory
A = W_ADDR
A = *A
*A = D
A = W_ADDR
*A = *A + 1

# Waiting for key release
LABEL WAIT_KEY_RELEASE
A = KBD
D = *A
A = WAIT_KEY_RELEASE
D ; JNE

# The key has released
A = WAIT_KEY_PRESS
JMP
```

### Escape Labyrinth

The computer is stuck in a labyrinth on Mars. Write a program that will make it escape the labyrinth.

The computer has connected wheels and a forward obstacle detector. Input/output to wheels and detector is memory-mapped on address 7FFF:

**Output signals to peripherals:**

| Bit | Bin      | Set to 1 to:            |
| --- | -------- | ----------------------- |
| 2   | 0b0100   | Move forward (1 step)   |
| 3   | 0b1000   | Turn left (90 degrees)  |
| 4   | 0b1_0000 | Turn right (90 degrees) |

The movement/turn is started when a bit is changing from 0 to 1, but will take a moment to complete.

**Input from peripherals:**

| Bit | Bin            | When 1                     |
| --- | -------------- | -------------------------- |
| 8   | 0b1_00000000   | Obstacle detected in front |
| 9   | 0b10_00000000  | Device is turning          |
| 10  | 0b100_00000000 | Device is moving forward   |

è¯»å†™éƒ½åœ¨å†…å­˜åœ°å€ 7FFFã€‚

æ³¨æ„ç§»åŠ¨å’Œæ—‹è½¬éƒ½ä¼šæŒç»­ä¸€æ®µæ—¶é—´ï¼Œéœ€è¦ç­‰å¾…åŠ¨ä½œå®Œæˆï¼Œå¦‚ï¼š

``` nasm
DEFINE IO 0x7FFF
DEFINE C_IS_BUSY 0x600

# Wait until idle
LABEL WHILE_BUSY
A = C_IS_BUSY
D = A
A = IO
D = D & *A
A = WHILE_BUSY
D ; JNE
```

èµ°è¿·å®«ç”¨ã€Œæ²¿å¢™èµ°æ³•ã€ï¼ˆä½¿ç”¨äºæ²¡æœ‰ç¯å½¢è·¯å¾„çš„è¿·å®«ï¼‰ã€‚æ¯”å¦‚é€‰å®šå³æ‰‹ ğŸ«±ï¼Œå•æ‰‹æ¨¡ä½ä¸€é¢å¢™å‡ºå‘ï¼Œæ‰‹å§‹ç»ˆä¸ç¦»å¼€å¢™é¢ã€‚

ä½†è¿™é‡Œéº»çƒ¦çš„ç‚¹æ˜¯å®ƒä¸ä¼šåˆ¤å®šå·¦è¾¹æˆ–å³è¾¹æ˜¯å¦æœ‰å¢™å£ï¼Œå°±å¾—ååå¤å¤ç¬¬å·¦å³è½¬åŠ¨â€¦â€¦åˆ¤æ–­å³æ‰‹æ˜¯å¦æ˜¯å¢™çš„åŠ¨ä½œå°±å˜æˆã€Œå‘å³è½¬ - åˆ¤æ–­å‰æ–¹æ˜¯å¦æœ‰å¢™ - å‘å·¦è½¬ã€ã€‚

- â‘  å¦‚æœå³æ‰‹æ˜¯å¢™ï¼Œå‰æ–¹æ²¡æœ‰å¢™ï¼Œåˆ™å‰è¿›ï¼›
- â‘¡ å¦‚æœå³æ‰‹æ˜¯å¢™ï¼Œå‰æ–¹ä¹Ÿæ˜¯å¢™ï¼Œåˆ™å·¦è½¬ï¼›
- â‚ å¦‚æœå³æ‰‹æ²¡æœ‰å¢™ï¼Œåˆ™å³è½¬ã€‚

æŒ‰ç…§è¿™ä¸ª computer çš„è¡Œä¸ºæ¨¡å¼åˆ™ä¸ºï¼š

- åˆ¤æ–­å‰æ–¹æ˜¯å¦æœ‰å¢™å¹¶è®°ä½
- å³è½¬
- åˆ¤æ–­å‰æ–¹æœ‰å¢™
  - è‹¥æ²¡æœ‰ï¼Œåˆ™å‰è¿›ï¼ˆå¯¹åº”ä¸Šè¾¹æƒ…å†µ â‘¢ï¼‰
  - è‹¥æœ‰ï¼Œåˆ™æ£€æŸ¥åˆšæ‰è®°å½•çš„å¼€å§‹æ—¶å‰æ–¹æ˜¯å¦æœ‰å¢™
    - è‹¥æœ‰ï¼Œåˆ™å·¦è½¬ä¸¤æ¬¡ï¼ˆå¯¹åº”ä¸Šè¾¹æƒ…å†µ â‘¡ï¼‰
    - è‹¥æ²¡æœ‰ï¼Œåˆ™å·¦è½¬ï¼Œç„¶åå‰è¿›ï¼ˆå¯¹åº”ä¸Šè¾¹æƒ…å†µ â‘ ï¼‰

``` nasm
# Assembler code
DEFINE IO 0x7FFF
DEFINE C_FRONT_WALL 0x100
DEFINE C_IS_BUSY 0x600
DEFINE C_MOVE_FORWARD 0x04
DEFINE C_TURN_LEFT 0x08
DEFINE C_TURN_RIGHT 0x10
DEFINE F_IS_WALL 0x100
DEFINE ONE_STACK 0xFF

LABEL GOTO_BEGIN

# Detect current front and remeber
A = C_FRONT_WALL
D = A
A = IO
D = D & *A
A = F_IS_WALL
*A = D

# Turn right
A = GOTO_1
D = A
A = ONE_STACK
*A = D
A = C_TURN_RIGHT
D = A
A = FUNC_EXEC_ACTION
JMP

LABEL GOTO_1

# Detect current front (previous right)
A = C_FRONT_WALL
D = A
A = IO
D = D & *A
A = CASE_3
D ; JEQ

# Check previous front status
A = F_IS_WALL
D = *A
A = CASE_1
D ; JEQ

# Case 2: turn left twice
LABEL CASE_2
A = GOTO_2
D = A
A = ONE_STACK
*A = D
A = C_TURN_LEFT
D = A
A = FUNC_EXEC_ACTION
JMP

LABEL GOTO_2

A = GOTO_BEGIN
D = A
A = ONE_STACK
*A = D
A = C_TURN_LEFT
D = A
A = FUNC_EXEC_ACTION
JMP

# Case 1: turn left then move forward
LABEL CASE_1
A = GOTO_3
D = A
A = ONE_STACK
*A = D
A = C_TURN_LEFT
D = A
A = FUNC_EXEC_ACTION
JMP

LABEL GOTO_3

A = GOTO_BEGIN
D = A
A = ONE_STACK
*A = D
A = C_MOVE_FORWARD
D = A
A = FUNC_EXEC_ACTION
JMP

# Case 3: move forward now
LABEL CASE_3
A = GOTO_BEGIN
D = A
A = ONE_STACK
*A = D
A = C_MOVE_FORWARD
D = A
A = FUNC_EXEC_ACTION
JMP

# A function
# Performs the move/turn action specified by D
# Waits until the move/turn complete
# Jump to *ONE_STACK
LABEL FUNC_EXEC_ACTION
A = IO
*A = D | *A
LABEL WHILE_BUSY
A = C_IS_BUSY
D = A
A = IO
D = D & *A
A = WHILE_BUSY
D ; JNE
A = ONE_STACK
A = *A
JMP
```

### Display

Display a logo (at least 16 pixels in both width and height) of your own choice on the screen.

The screen is 512 x 256 monochrome pixels, memory-mapped from address 0x4000 to 0x6000. Each address correspond to 16 pixel on the screen. The lines are contiguously in memory, so first line start at 0x4000, second line starts at 0x4020 and so forth.

åšä¸€ä¸ª 32 x 32 çš„ logo æ”¾åœ¨å±å¹•ä¸­å¤®ã€‚Logo ç¬¬ä¸€è¡Œå†…å­˜åœ°å€ä¸º 0x4E0Fã€0x4E10ï¼Œä¸‹ä¸€è¡Œæ˜¯ 0x4E2F å’Œ 0x4E30ï¼Œç„¶åæ˜¯ 0x4E4F å’Œ 0x4E50ï¼Œâ€¦â€¦

å¦å¤–éœ€è¦æ³¨æ„ï¼Œä»¥ data instruction æ¨¡å¼å¾€å¯„å­˜å™¨ A å†™æ•°æ®çš„æ—¶å€™ï¼Œæœ€é«˜ä½è¢« **ci** flag å ç”¨ï¼Œæ— æ³•å†™å…¥å¤§äºç­‰äº 0x8000 çš„æ•°å­—ã€‚æ‰€ä»¥å¯¹äºå¤§äºç­‰äº 0x8000 çš„æ•°å€¼ï¼Œéœ€è¦å…ˆå°†å…¶åç å†™å…¥å¯„å­˜å™¨ Aï¼Œç„¶ååœ¨æ‰§è¡Œ bitwise inversion æ“ä½œå¾—åˆ°æƒ³è¦çš„æ•°å­—ã€‚

> [!tip]
> ä»¥ä¸‹ä»£ç å®Œå…¨ç”± AI ç”Ÿæˆã€‚
>
> æŠŠ PNG å›¾ç‰‡ç»™ AIï¼Œè®©å®ƒç¼©æ”¾åç”Ÿæˆ 32 è¡Œã€æ¯è¡Œä¸¤ä¸ª 16-bit hex çš„æ•°æ®ã€‚å†æŠŠç¬¬ä¸€è¡Œæ•°æ®çš„å¤„ç†ä»£ç ç¤ºä¾‹ä»¥åŠæ¯ä¸€è¡Œçš„åœ°å€å˜åŒ–è§„å¾‹å‘Šè¯‰å®ƒï¼ŒæŠŠå¤§æ•°å­—çš„å–åè§„åˆ™å‘Šè¯‰å®ƒï¼Œè®©å®ƒç”Ÿæˆå®Œæ•´çš„ä»£ç ã€‚

``` nasm

# Generated assembly instructions from hex data
# Each line processes two 16-bit hex numbers and stores them at incrementing addresses

# Line 1: 0400 0020
A = 0x0400
D = A
A = 0x4E0F
*A = D
A = 0x0020
D = A
A = 0x4E10
*A = D

# Line 2: 0E00 0070
A = 0x0E00
D = A
A = 0x4E2F
*A = D
A = 0x0070
D = A
A = 0x4E30
*A = D

# Line 3: 0700 00E0
A = 0x0700
D = A
A = 0x4E4F
*A = D
A = 0x00E0
D = A
A = 0x4E50
*A = D

# Line 4: 078F F1E0
A = 0x078F
D = A
A = 0x4E6F
*A = D
A = 0x0E1F
D = ~A
A = 0x4E70
*A = D

# Line 5: 07FF FFE0
A = 0x07FF
D = A
A = 0x4E8F
*A = D
A = 0x001F
D = ~A
A = 0x4E90
*A = D

# Line 6: 03FF FFC0
A = 0x03FF
D = A
A = 0x4EAF
*A = D
A = 0x003F
D = ~A
A = 0x4EB0
*A = D

# Line 7: 03FF FFC0
A = 0x03FF
D = A
A = 0x4ECF
*A = D
A = 0x003F
D = ~A
A = 0x4ED0
*A = D

# Line 8: 03FF FFC0
A = 0x03FF
D = A
A = 0x4EEF
*A = D
A = 0x003F
D = ~A
A = 0x4EF0
*A = D

# Line 9: 07FE 7FE0
A = 0x07FE
D = A
A = 0x4F0F
*A = D
A = 0x7FE0
D = A
A = 0x4F10
*A = D

# Line 10: 07F0 0FE0
A = 0x07F0
D = A
A = 0x4F2F
*A = D
A = 0x0FE0
D = A
A = 0x4F30
*A = D

# Line 11: 0FE0 07F0
A = 0x0FE0
D = A
A = 0x4F4F
*A = D
A = 0x07F0
D = A
A = 0x4F50
*A = D

# Line 12: 0FC0 03C0
A = 0x0FC0
D = A
A = 0x4F6F
*A = D
A = 0x03C0
D = A
A = 0x4F70
*A = D

# Line 13: 1F80 0100
A = 0x1F80
D = A
A = 0x4F8F
*A = D
A = 0x0100
D = A
A = 0x4F90
*A = D

# Line 14: 1F00 0000
A = 0x1F00
D = A
A = 0x4FAF
*A = D
A = 0x0000
D = A
A = 0x4FB0
*A = D

# Line 15: 1F00 0000
A = 0x1F00
D = A
A = 0x4FCF
*A = D
A = 0x0000
D = A
A = 0x4FD0
*A = D

# Line 16: 1F00 0000
A = 0x1F00
D = A
A = 0x4FEF
*A = D
A = 0x0000
D = A
A = 0x4FF0
*A = D

# Line 17: 3F00 0000
A = 0x3F00
D = A
A = 0x500F
*A = D
A = 0x0000
D = A
A = 0x5010
*A = D

# Line 18: 3F00 0000
A = 0x3F00
D = A
A = 0x502F
*A = D
A = 0x0000
D = A
A = 0x5030
*A = D

# Line 19: 3F00 0000
A = 0x3F00
D = A
A = 0x504F
*A = D
A = 0x0000
D = A
A = 0x5050
*A = D

# Line 20: 1F00 0000
A = 0x1F00
D = A
A = 0x506F
*A = D
A = 0x0000
D = A
A = 0x5070
*A = D

# Line 21: 1F00 0000
A = 0x1F00
D = A
A = 0x508F
*A = D
A = 0x0000
D = A
A = 0x5090
*A = D

# Line 22: 1F80 0000
A = 0x1F80
D = A
A = 0x50AF
*A = D
A = 0x0000
D = A
A = 0x50B0
*A = D

# Line 23: 1F80 0180
A = 0x1F80
D = A
A = 0x50CF
*A = D
A = 0x0180
D = A
A = 0x50D0
*A = D

# Line 24: 0FC0 03E0
A = 0x0FC0
D = A
A = 0x50EF
*A = D
A = 0x03E0
D = A
A = 0x50F0
*A = D

# Line 25: 0FE0 07F0
A = 0x0FE0
D = A
A = 0x510F
*A = D
A = 0x07F0
D = A
A = 0x5110
*A = D

# Line 26: 07F8 1FE0
A = 0x07F8
D = A
A = 0x512F
*A = D
A = 0x1FE0
D = A
A = 0x5130
*A = D

# Line 27: 07FF FFE0
A = 0x07FF
D = A
A = 0x514F
*A = D
A = 0x001F
D = ~A
A = 0x5150
*A = D

# Line 28: 03FF FFC0
A = 0x03FF
D = A
A = 0x516F
*A = D
A = 0x003F
D = ~A
A = 0x5170
*A = D

# Line 29: 01FF FF80
A = 0x01FF
D = A
A = 0x518F
*A = D
A = 0x007F
D = ~A
A = 0x5190
*A = D

# Line 30: 007F FE00
A = 0x007F
D = A
A = 0x51AF
*A = D
A = 0x01FF
D = ~A
A = 0x51B0
*A = D

# Line 31: 003F FC00
A = 0x003F
D = A
A = 0x51CF
*A = D
A = 0x03FF
D = ~A
A = 0x51D0
*A = D

# Line 32: 0003 C000
A = 0x0003
D = A
A = 0x51EF
*A = D
A = 0x3FFF
D = ~A
A = 0x51F0
*A = D
```

![my logo](20250618-214751.png)

### Network

Receive data from another computer over the network and display it on the screen.

The payload will be an image 16 pixels in width.

The network wires are memory mapped to the addressÂ `6001`Â (hex), with two significant bits:Â **data**Â (bit 0) which is the current bit of data sent over the wire andÂ **sync**Â (bit 1) which change to indicate that a new bit has arrived.

> Each time the sync signal changes (from 0 to 1 or from 1 to 0), a new bit can be read from the data wire.

The protocol (in this mission) is that a transmission always starts with a 1 bit, followed by 16 bits of data, then followed by a control bit. If the control bit is 0, it means the transmission has ended. If the control bit is 1, it means another 16 bits data will follow, again followed by a control bit. And so on.

``` nasm
DEFINE C_NET_ADDR 0x6001
DEFINE C_NET_DATA 0x01
DEFINE C_NET_SYNC 0x02
DEFINE C_STACK_BOTTOM_ADDR 0x1FF

DEFINE V_STACK_TOP_ADDR 0x100
DEFINE V_WRITE_ADDR 0x101
DEFINE V_IMG_WORD 0x102
DEFINE V_BIT_MASK 0x103
DEFINE V_PREV_NET_SYNC 0x104
DEFINE V_NET_DATA 0x105

# Init net `sync` state
A = C_NET_ADDR
D = *A
A = C_NET_SYNC
D = D & A
A = V_PREV_NET_SYNC
*A = D

# Init stack
A = C_STACK_BOTTOM_ADDR
D = A
A = V_STACK_TOP_ADDR
*A = D

# Init image write address
A = 0x4000
D = A
A = V_WRITE_ADDR
*A = D

LABEL L_IMAGE_WORD_LOOP

# Call F_RECEIVE_BIT to receive the control bit
A = L_001
D = A
A = V_STACK_TOP_ADDR
*A = *A + 1
A = *A
*A = D
A = F_RECEIVE_BIT
JMP

LABEL L_001

# End if no more data
A = V_NET_DATA
D = *A
A = L_END
D ; JEQ

# Init for receiving a word
A = V_IMG_WORD
*A = 0
A = V_BIT_MASK
*A = 1

# Loop to receive 16 bits
LABEL L_RECEIVE_BIT_LOOP

# Call F_RECEIVE_BIT to receive a single bit
A = L_002
D = A
A = V_STACK_TOP_ADDR
*A = *A + 1
A = *A
*A = D
A = F_RECEIVE_BIT
JMP

LABEL L_002

# Skip if data is `0`
A = V_NET_DATA
D = *A
A = L_AFTER_BIT
D ; JEQ

# Merge the received bit into the word
A = V_BIT_MASK
D = *A
A = V_IMG_WORD
*A = D | *A

LABEL L_AFTER_BIT

# Shift the bit mask left
A = V_BIT_MASK
D = *A
D = D + *A
*A = D

# Check if we have received 16 bits
A = L_RECEIVE_BIT_LOOP
D ; JNE

# Write the received word to the image
A = V_IMG_WORD
D = *A
A = V_WRITE_ADDR
A = *A
*A = D

# Increment the write address
A = 0x20
D = A
A = V_WRITE_ADDR
*A = D + *A

A = L_IMAGE_WORD_LOOP
JMP


# Function
# Receives a single bit from net
LABEL F_RECEIVE_BIT

# Wait for next bit
LABEL L_WAIT_NET_DATA
A = C_NET_ADDR
D = *A
A = C_NET_SYNC
D = D & A
A = V_PREV_NET_SYNC
D = D ^ *A
A = L_WAIT_NET_DATA
D ; JEQ

# Store the current net `sync`
LABEL F_STORE_NET_SYNC
A = C_NET_ADDR
D = *A
A = C_NET_SYNC
D = D & A
A = V_PREV_NET_SYNC
*A = D

# Store the current net data
A = C_NET_ADDR
D = *A
A = C_NET_DATA
D = D & A
A = V_NET_DATA
*A = D

# Return
A = V_STACK_TOP_ADDR
A = *A
D = *A
A = V_STACK_TOP_ADDR
*A = *A - 1
A = D
JMP


LABEL L_END
```

## Software Level: Stack machine

### Init stack

The stack is an area of memory where we can store and retrieve intermediate values in a last-in-first-out manner.

We use the first available memory address, address 0, to store theÂ **stack pointer**Â (or SP).

We write this in the form of aÂ **macro**Â calledÂ `init.stack`. A macro is a snippet of code which can be easily reused. If the keywordÂ `init.stack`Â is used in assembler, it will be replaced with this code.

Set the Stack Pointer (RAM address 0) to 256 (Hex value 0100).

It may be helpful to define a constant named SP with the value 0.

- SP (Stack Pointer): å€¼ä¸º 0 çš„å¸¸é‡ï¼Œå³ `mem[0]` è®°å½•å½“å‰çš„æ ˆé¡¶ã€‚
- åˆå§‹æ ˆé¡¶åœ°å€ä¸º 0x0100ï¼ˆè¿™é‡Œæ ˆé¡¶æ˜¯å¯ä»¥å†™å…¥çš„ä½ç½®ï¼‰ã€‚

Constants:

- SP: 0

``` nasm
# init.stack
A = 0x0100
D = A
A = SP
*A = D
```

### Push D

Storing a new value on the stack is called pushing.

Write code which pushes the current value of the D-register on the top of the stack.

The SP should be increased by one.

SP points to the address after the top of the stack.

``` nasm
# push.D
A = SP
A = *A
*A = D
A = SP
*A = *A + 1
```

### Pop D

Retrieving the value at the top of the stack is calledÂ **popping**Â a value.

Write code which pops the value at top of the stack and writes it to theÂ **D**-register.

The stack pointer (SP) should be decreased by 1 when a value is popped.

SP points to the address after the top of the stack, so the value to retrieve is at SP - 1.

``` nasm
# pop.D
A = SP
*A = *A - 1
A = *A
D = *A
```

### Pop A

Write code which pops the value at top of the stack and writes it to theÂ **A**-register.

**Important criteria**: TheÂ **D**-register must not be affected by this operation.

``` nasm
# pop.A
A = SP
*A = *A - 1
A = *A
A = *A
```

### Push Value

Now we introduce a macro which use aÂ _placeholder_.

The macro keywordÂ `push.value`Â must followed by a number, e.g.Â `push.value 42`.

When the macro is used, the placeholder keywordÂ `value`Â in the macro code will be replaced with the specified number, i.e.Â `42`.

``` nasm
# push.value <value>
A = value
D = A
push.D
```

### Add

Pop two values from the stack, add them, and push the sum on the stack.

``` nasm
# add
pop.D
pop.A
D = D + A
push.D
```

### Sub

Pop two values from stack, subtract the first from the second, and then push the result back on the stack.

{% invert %}

![|120](20250622-224209.png)

{% endinvert %}

â€œsubtract the first from the secondâ€ = ã€Œä»ç¬¬äºŒä¸ªæ•°ä¸­å‡å»ç¬¬ä¸€ä¸ªæ•°ã€ï¼Œå³ **åå‡ºæ ˆçš„æ•° - å…ˆå‡ºæ ˆçš„æ•°**ã€‚

å› ä¸ºå‡ºæ ˆæ˜¯ macro å†…éƒ¨é€»è¾‘ï¼Œä»ä½¿ç”¨è€…çš„è§’åº¦ï¼Œå°±æ˜¯ **å…ˆå…¥æ ˆçš„æ•° - åå…¥æ ˆçš„æ•°**ã€‚

``` nasm
# sub
pop.D
pop.A
D = A - D
push.D
```

### Neg

Negate the value on top of the stack.

``` nasm
# neg
pop.D
D = -D
push.D
```

### And

Pop two values from stack, perform a bitwise AND and push the result back on the stack.

``` nasm
# and
pop.D
pop.A
D = D & A
push.D
```

### Or

Pop two values from stack, perform a bitwise OR and push the result back on the stack.

``` nasm
# or
pop.D
pop.A
D = D | A
push.D
```

## Software Level: High-level language

A high level language have a more human-friendly and flexible syntax which is thenÂ _compiled_Â into machine code instructions. For example the high-level codeÂ `2 + 2`Â could be compiled into the low-level code:

``` nasm
push.value 2
push.value 2
ADD
```

Compilation has three stages:

1. (1) Tokenization
2. (2) Parsing
3. (3) Code generation

### Tokenize

The tokenizer is preconfigured to recognize numbers and the symbol '+'.

Configure the tokenizer to additionally recognize the symbols minus '-' and parentheses '(' and ')'.

Token type:

- **Exact** matches match the exact text specified under `Match`. Multiple exact matches can be specified in the same box, separated by whitespace.
- **Pattern** can use character groups in brackets and quantifiers `*` and `+`.
        - Example: `[0-9]` matches a decimal digit, `[0-9]+` matches one or more digits.

Gramma property:

- **Ignore** patterns are skipped by the tokenizer. Use e.g. for whitespace and comments.
- **Name** patterns are represented with the specified name in the grammar.
- **Literal** matches are represented with the literal text in the grammar.

Token definitions:

| Type    | Match     | Grammar | Token name |
| ------- | --------- | ------- | ---------- |
| Pattern | `[ ]+`    | Ignore  |            |
| Pattern | `[0-9]+`  | Name    | Number     |
| Exact   | `+ - ( )` | Literal |            |

### Grammar

Parse the sequence of tokens into a syntax-tree.

The syntax of a high-level language is described through aÂ **grammar**.

A grammar is a set of rules where each rule names a part of the syntax and defines how it composed.

The terms used in the grammar are called symbols. The rules define how a symbol (left of the arrow) is composed of one or more other symbols (right of the arrow). The symbols the right of the arrow are either tokens which is defined by the token specification (in the previous step), or they are symbols themselves defined by rules in the same grammar.

The symbols representing tokens (likeÂ `Number`Â andÂ `+`) are calledÂ _terminals_, the symbols like Program and Expression which are defined by other rules in the grammar are calledÂ _non-terminals_.

The names used as non-terminal symbols are arbitrary â€“ you can use names which makes sense for you. Only condition is there must be a "starting symbol" calledÂ `Program`, which represent the whole program.

This game uses an Earley-parser, which is not the fastest but which is flexible and easy to write a grammar for.

Define aÂ **Grammar**Â for expressions involving numbers, parentheses and the operatorsÂ `+`Â andÂ `-`.

The start symbol isÂ `Expression`.

An expression should correspond to one of:

- AÂ `Number`Â token
- _Expression_Â `+`Â _Expression_
- _Expression_Â `-`Â _Expression_
- `-`Â _Expression_
- `(`Â _Expression_Â `)`

Grammar:

> [!caution]
> æœ¬å…³ä¸­çš„é”™è¯¯ï¼Œåœ¨ check solution æ—¶å¯èƒ½æ— æ³•æŒ‡å‡ºï¼Œåˆ°ä¸‹ä¸€å…³ code generation æ—¶å¯èƒ½ä¼šé‡åˆ°é—®é¢˜ï¼Œéœ€è¦å†å›æ¥ä¿®æ”¹ã€‚

- Expression â†’ `Number`
- Expression â†’ `Number`
- Expression â†’ `( Expression )`
- Expression â†’ `Expression + Expression`
- Expression â†’ `Expression - Expression`

### Code generation

The third step in the compilation is to generate machine code from the syntax tree.

This is done by associating each syntax rule with a block of assembler code.

The compiler then generate the resulting code by traversing the syntax tree and for each node in the tree generate the code associated with the rule.

Define code-generation for the syntax rules of the language, to support addition and subtraction.

Syntax rules:

``` nasm
# Expression â†’ `Number`
push.value [Number]

# Expression â†’ `Number`
[Expression]
neg

# Expression â†’ `( Expression )`
[Expression]

# Expression â†’ `Expression + Expression`
[Expression 1]
[Expression 2]
add

# Expression â†’ `Expression - Expression`
[Expression 1]
[Expression 2]
sub
```

æœ‰ä¸ªé—®é¢˜æ˜¯å®ƒä¸ä¼šéµå¾ªä»å·¦å‘å³çš„è®¡ç®—é¡ºåºï¼Œæ¯”å¦‚`100 - 2 + 2 - (7 + 10)` ä¼šå¾—åˆ°å¦‚ä¸‹çš„è¡¨è¾¾å¼ï¼Œç»“æœæ˜¯ 113 è€Œä¸æ˜¯æœŸæœ›çš„ 83ã€‚

{% invert %}

![|480](20250622-215112.png)

{% endinvert %}

## Software Level: Conditonals

### Eq

Pop the two top values from the stack and compare them. If they are equal, push the value -1 (`FFFF`Â in hex). Otherwise pushÂ `0`.

In conditionals,Â `FFFF`Â representsÂ _true_Â andÂ `0`Â representsÂ _false_.

``` nasm
# eq
pop.D
pop.A
D = D - A
A = WHEN_TRUE
D ; JEQ
push.value 0
A = END
JMP

LABEL WHEN_TRUE
D = 0
D = ~D
push.D

LABEL END
```

### Gt

Pop the two top values from the stack and compare them. If the first is greater than the second, push the value -1 (`FFFF`Â in hex). Otherwise push 0.

{% invert %}

![|120](20250622-223615.png)

{% endinvert %}

è¿™é‡Œæ–‡å­—æè¿°è·Ÿå›¾ç‰‡æœ‰å‡ºå…¥ã€‚æ–‡å­—è¯´çš„æ˜¯å¦‚æœ **å…ˆå‡ºæ ˆçš„æ•° > åå‡ºæ ˆçš„æ•°**ï¼Œç»“æœä¸º -1ã€‚ä½†å›¾ä¸­å…ˆå‡ºæ ˆ 5ï¼Œå¤§äºåå‡ºæ ˆçš„ 3ï¼Œç»“æœå´ä¸º 0ã€‚

"Test code" çš„æç¤ºè·Ÿæ–‡å­—æè¿°çš„é€»è¾‘ä¸€è‡´ã€‚

ä½† "Check solution" çš„æ£€æŸ¥é€»è¾‘ä¼¼ä¹è·Ÿå›¾ä¸€è‡´ï¼Œå³å½“ **å…ˆå…¥æ ˆçš„æ•° > åå…¥æ ˆçš„æ•°**ï¼Œç»“æœä¸º -1ï¼Œå¦åˆ™ä¸º 0ã€‚è¿™ä¸ªå…¶å®è·Ÿ **sub** çš„è¡Œä¸ºï¼ˆ**å…ˆå…¥æ ˆçš„æ•° - åå…¥æ ˆçš„æ•°**ï¼‰ä¹Ÿæ˜¯ç±»ä¼¼çš„ã€‚

``` nasm
# gt
pop.D
pop.A
D = A - D
A = WHEN_TRUE
D ; JGT
push.value 0
A = END
JMP

LABEL WHEN_TRUE
D = 0
D = ~D
push.D

LABEL END
```

### Lt

Pop the two top values from the stack and compare them. If the first is less than the second, push the value -1 (`FFFF`Â in hex). Otherwise push 0.

åŒæ ·ï¼Œè¿™é‡Œåº”è¯¥æ˜¯æŒ‡å½“ **å…ˆå…¥æ ˆçš„æ•° < åå…¥æ ˆçš„æ•°**ï¼Œç»“æœä¸º -1ï¼Œå¦åˆ™ä¸º 0ã€‚

``` nasm
# lt
pop.D
pop.A
D = A - D
A = WHEN_TRUE
D ; JLT
push.value 0
A = END
JMP

LABEL WHEN_TRUE
D = 0
D = ~D
push.D

LABEL END
```

### Not

Invert the value on top of the stack, using bitwise inversion.

``` nasm
# not
pop.D
D = ~D
push.D
```

### Goto

Jump to the label given in the placeholder.

``` nasm
goto <label>
A = label
JMP
```

### If-goto

Pop the value on top of the stack.

Jump to the label if it is non-zero.

``` nasm
# if.goto <label>
pop.D
A = label
D ; JNE
```

## Software Level: Memory

### Push Memory

The value on top of the stack is a memory address.

Pop the address from the stack. Fetch the current contents of the memory address, and push this on the stack.

``` nasm
# push.memory
pop.A
D = *A
push.D
```

### Pop Memory

Pop two values value from the stack. The second value is a memory address.

Write the first value to memory at the given address.

ä»ä½¿ç”¨è€…çš„è§’åº¦ï¼Œ**å…ˆå…¥æ ˆåœ°å€ï¼Œåå…¥æ ˆæ•°æ®**ã€‚

``` nasm
# pop.memory
pop.D
pop.A
*A = D
```

### Push Static

Take the current contents of the memory address given by theÂ `address`Â placeholder and push it on the stack.

``` nasm
# push.static <address>
A = address
D = *A
push.D
```

### Pop Static

Take the value on top of the stack and store it at the memory address given by theÂ `address`Â placeholder.

``` nasm
# pop.static <address>
pop.D
A = address
*A = D
```

## Software Level: Functions

TheÂ **function**Â is perhaps the single most important abstraction in software.

A function is a unit of code which take some input (called arguments), has some local storage to use for processing, and return a value.

A function can be executed (called) from anywhere in the program. When a function is called, the address of the call is stored on the stack, and when the function is complete, it returns to the address from where it was called

Arguments and local storage is also stored on the stack.

Functions require three segments to work together:

- Call where a function is called from somewhere in the program.
- Function which is the start of the function
- Return which is the end of the function.

These three units need to work together according to a shared convention for how data is passed in and out of the function.

### Call

TheÂ **call**Â macro invokes a function. It should prepare the stack for the call, jump to the given function label, and afterwards restore the state.

Before the call, zero or more values are placed on the stack. The placeholderÂ **argumentCount**Â is the number of arguments

The calling convention requires three shared memory slots:

- `ARGS = 1`
  - Address of the arguments for the current function
- `LOCALS = 2`
  - Address of the local storage for the function
- `RETVAL = 6`
  - Temporary slot for the value returned from a function.

(These slot addresses can be defined as shared constants for convenience.)

Steps:

- Push the current value of ARGS on the stack.
- Push the current value of LOCALS.
- Push the address immediately after the jump (the return address).
- Calculate a new ARGS address which is the current SP minusÂ **argumentCount**Â minus 3 (because we just pushed three values on the stack).
- Jump to the address given by theÂ **functionName**Â placeholder.

After the function call is executed, control will return to the label following the jump.

- Store the current ARGS value in a temporary slot.
- Restore the LOCALS value from the stack
- Restore the ARGS value from the stack
- Set SP to the previous ARGS value â“ Why not use `SP - argumentCount`
- Push RETVAL on the stack

``` nasm
# call <functionName> <argumentCount>
push.static ARGS
push.static LOCALS
push.value AFTER_CALL

# Calculate a new ARGS address
A = SP
D = *A
A = argumentCount
D = D - A
A = 3
D = D - A
A = ARGS
*A = D

A = functionName
JMP

# After the function call is executed
LABEL AFTER_CALL

# Store the current ARGS value in a temporary slot
DEFINE TEMP_SLOT 0x10
A = ARGS
D = *A
A = TEMP_SLOT
*A = D

pop.static LOCALS
pop.static ARGS

# Set SP to the previous ARGS value
A = TEMP_SLOT
D = *A
A = SP
*A = D

push.static RETVAL
```

### Function

TheÂ **Function**Â macro defines the top of the function block. It should adjust the stack to make space for local storage. The size of the local storage is given in the placeholderÂ **localsCount**

- A label with the name given in the placeholderÂ **functionName**Â should start the block.
- Set LOCALS to the current SP.
- Make space for local data on the stack by addingÂ **localsCount**Â to the currentÂ **SP**Â value.

``` nasm
# function <functionName> <localsCount>
LABEL functionName

A = SP
D = *A
A = LOCALS
*A = D

A = localsCount
D = A
A = SP
D = D + *A
*A = D
```

### Return

TheÂ **Return**Â macro defines the end of the function block. It should store the return value in the memory address RETVAL and restore the stack.

- Pop the top value from the stack into the RETVAL memory slot.
- Set SP to the value of LOCALS
- Pop the return address from the stack and jump to it.

``` nasm
# return
pop.static RETVAL

A = LOCALS
D = *A
A = SP
*A = D

pop.A
JMP
```

### Push argument

Take the current contents of the memory address given byÂ `ARGS`Â + theÂ **index**Â placeholder and push it on the stack.

``` nasm
# push.argument <index>
A = ARGS
D = *A
A = index
D = D + A
push.D
push.memory
```

### Pop argument

Take the value on top of the stack and store it at the memory address given by the value ofÂ `ARGS`Â + theÂ **index**Â placeholder.

``` nasm
# pop.argument <index>
A = ARGS
D = *A
A = index
D = D + A

pop.A

# Needs exchange the value of A and D, since currently D holds the address, A holds the data
A = D ^ A
D = D ^ A
A = D ^ A

*A = D
```

### Push local

Take the current contents of the memory address given byÂ `LOCALS`Â + theÂ **index**Â placeholder and push it on the stack.

``` nasm
# push.local <index>
A = LOCALS
D = *A
A = index
D = D + A
push.D
push.memory
```

### Pop local

Take the value on top of the stack and store it at the memory address given by the value ofÂ `LOCALS`Â + theÂ **index**Â placeholder.

``` nasm
# pop.local <index>
A = LOCALS
D = *A
A = index
D = D + A

pop.A

# Needs exchange the value of A and D, since currently D holds the address, A holds the data
A = D ^ A
D = D ^ A
A = D ^ A

*A = D
```
